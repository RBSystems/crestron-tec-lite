//Module Info
/*
	Name: Bradley T Streeter / David Allred
	Company: Brigham Young University
	
	TPCS-4SMD Version
	
*/

//Compiler Directives
#CATEGORY "46" "Monitoring"  //Custom
#Default_Volatile	//All variables are in volitile memory (will be erased upon reboot)
#Enable_Stack_Checking
#Enable_Trace

//Input output variables

Digital_Input Add_Slave,Remove_Slave,Reset_Program,Test,Get_Program_Info; 
Buffer_Input IP_Address$[15],From_Console$[3000];
String_Output _skip_,_skip_,_skip_,_skip_,Program_Name$,Compile_Date$,To_Console$, IPTable$, CPver$, CPHostname$, CPIP$, CPMAC$;

//Global variables

String IP_ID$[2],Port$[4];

//Parameters

//Parameter Properties

//Sockets

//Functions

//Events-Push-Release-Change

Push Add_Slave

	{
	To_Console$ = "ADDSlave " + IP_ID$ + " " + IP_Address$ + "\x0D";
	IP_Address$ ="";
	}

Push Remove_Slave
	{
	To_Console$ = "REMSlave " + IP_ID$ + " " + IP_Address$ + "\x0D";
	}
	
Push Reset_Program
	{
	To_Console$ = "ProgReset" + "\x0D";
	}
	
Push Test
	{
	To_Console$ = "IPTable -I:" + IP_ID$ + "\x0D";
	}

Push Get_Program_Info
	{
	To_Console$ = "ProgComments\x0D";	//Request Running Program Info
	To_Console$ = "ver\x0D";	//request TPCS-4SMD Firmware version
	To_Console$ = "host\x0D";	//request hostname
	To_Console$ = "ipaddress\x0D"; // request JUST the ip address
	To_Console$ = "estatus\x0D"; // request ethernet information, including MAC address
	}

Change From_Console$
	{
	Integer StartPosition;
	String in$[3000], TempString[100], Trash[250];
	
	in$ = Gather("TPCS-4SMD>", From_Console$);
//	Print("in$ = %s",in$);
//	Print("Length of in$ = %d\n", Len(in$));
	if (Find("IP Table:",in$)&& (len(in$)<150))
		{
		//Print("Found IPTable ID:7");
		IPTable$ = Left(in$,(Find("TPCS-4SMD>",in$) - 5));
		in$ = "";
		}
		else if (find("TPCS-4SMD Cntrl Eng", in$))
		{
		CPver$ = Left(in$, (find("TPCS-4SMD>",in$) - 5));
		in$ = "";
		}
	
	While(Find("\x0A",In$))
		{
		TempString = Remove("\x0A",In$);
		if (Find("Program File:",TempString))
			{
			Trash = Remove("Program File: ",TempString);
			Program_Name$ = Left(TempString,Find("\x0A",Tempstring) - 2);
			}
			
		else if (Find("Compiled On:",TempString))
			{
			Trash = Remove("Compiled On:  ",TempString);
			Compile_Date$ = Left(TempString,Find("\x0A",Tempstring) - 2);
			}
		else if (Find("Host Name:",TempString))
			{
			Trash = Remove("Host Name: ",TempString);
			CPHostname$ = Left(TempString,Find("\x0D",Tempstring) - 1);
			}
		else if (Find("IP address:",TempString))
			{
			Trash = Remove("IP address:",TempString);
			CPIP$ = Left(TempString,Find("\x0D",TempString) - 1);
			
			}
		else if (Find("MAC Address(es):",TempString))
			{
			Trash = Remove("MAC Address(es): ",TempString);
			CPMAC$ = Left(TempString,Find("\x0D",Tempstring) - 1);			
			}

		}
	}

//Function Main

Function Main()	//Gets run at startup

	{
	WaitForInitializationComplete();
	IP_ID$ = "07";
	Port$ = "2202";
	Program_Name$ = "";
	Compile_Date$ = "";
	}
